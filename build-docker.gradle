/*
 * Copyright 2019 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:6.0.0'
    }
}

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import org.apache.tools.ant.filters.ReplaceTokens

docker {
    url.set("${dockerServerUrl}")
}

def dockerProjectName = "reportportal-dev/${project.name}".toLowerCase()

task prepareDockerfile(type: Copy) {
    /* copy dockerfile and replace version of JAR */
    def dockerFileName = isDebugMode ? "Dockerfile-test" : "Dockerfile-develop"
    from "docker/${dockerFileName}"
    into "${project.buildDir}/libs"
    filter(ReplaceTokens, tokens: project.properties.findAll { it.value instanceof String }.collectEntries {
        [it.key, it.value]
    })
    rename(dockerFileName, 'Dockerfile')
}

task buildDocker(type: DockerBuildImage, dependsOn: prepareDockerfile) {
    inputDir.set(file("${project.buildDir}/libs"))
    images.add("${dockerProjectName}:latest")
}

prepareDockerfile.dependsOn build
buildDocker.dependsOn prepareDockerfile

